u = fpp_library("util.lib");

// Much simpler than fi.dynamicSmoothing() and works just as well.
//
// https://cytomic.com/files/dsp/DynamicSmoothing.pdf
// diff:
//	- use 'sens' as is
//	- new arg 'fmax' to avoid the hardcoded '1' which corresponds
//	  to fmax = ma.SR/4 in this code. Allows to damp overshoots.
//
__dynasmoo(fc, fmax, sens) = tick ~ (_,_) with {
	g0(f) = 2 * gc / (1 + gc) with {
		gc = tan(ma.PI / ma.SR * f);
	};

	tick(lp1z,lp2z, inp) = lp1,lp2 with {
		g = g0(fc) + sens * abs(lp1z - lp2z)
			: min(g0(fmax));

		lp1 = lp1z + g * (inp - lp1z);
		lp2 = lp2z + g * (lp1 - lp2z);
	};
};

dynasmoo = __dynasmoo : !,_;

__glide(mode, tt1, tgt) = tick ~ (_,_) with {
	// acceleration to pass the distance d in time t
	__max_dv(d,t) = 4 * d / (t * ma.SR) ^ 2;
	// using d=1, distance d is passed in time tt1 * sqrt(d)
	// use tt1 / sqrt(d) to pass the distance d in time tt1
	max_dv = __max_dv(1, tt1);

	// discrete integration, imprecise.
	dx_vo(0,dx) = sqrt(2 * max_dv * abs(dx));
	// discrete time, more precise but slower.
	dx_vo(1,dx) = max_dv/2 * (sqrt(8/max_dv * abs(dx) + 1) - 1);
	// same but avoids catastrophic cancellation when dx is small
	dx_vo(2,dx) = 4 * abs(dx) / (sqrt(8/max_dv * abs(dx) + 1) + 1);

	tick(x,v) = x + v1, v1 with {
		dx = tgt - x;
		// max speed to reach tgt with v=0 using deceleration -max_dv.
		// min(dx) to avoid overshoot if we can reach tgt in this step.
		vo = dx : dx_vo(mode) : min(abs(dx)) : cp_sign(dx);

		dv = vo - v;
		v1 = select2(abs(dv) <= max_dv, v + cp_sign(dv, max_dv), vo);
	};

	cp_sign(x,y) = ma.copysign(y,x);
};

glide = __glide(1) : _,!;

// ----------------------------------------------------------------------------
// relative standard deviation
stat_rsd(a,v) = a, select2(abs(a) > ma.EPSILON, 0, sqrt(v)/abs(a));

// for ba.time or const input:
// s = 1 - 2/(N+1)			 "exact" mean
// s = 1 - (sqrt(12*(N^2+2))-6)/(N^2-1)  "exact" variance
//
__smoo_stat = FPP(upd, s, x) {
decl:	float	$avg = 0, $var = 0; //reset
exec:
	float s = $s, x = $x;
	float diff = x - $avg;
	float incr = (1-s) * diff;
	int upd = $upd;
	if (upd < 0)
		$avg = $var = 0;
	else if (upd > 0) {
		$avg += incr;
		$var = s * ($var + diff * incr);
	}
	[=$avg, =$var];
};

smoo_stat = __smoo_stat : stat_rsd;

// ----------------------------------------------------------------------------
// +-1 after reset, then +-2
range_cross = FPP(lo,hi, in) pure _int_ {
decl:	int $st = 0;
exec:	auto lo = $lo; auto hi = $hi;
	auto in = $in; int  st = $st;

	if	(in < lo) $st = -1;
	else if (in > hi) $st = +1;

	$st - st;
};

// prev value was local extremum
extremum = _ <: mem-_ : range_cross(0,0);

env_foll2 = FPP(rel, in) {
decl:	float $lo = 0, $hi = 0;
exec:	float p  = $(ba.tau2pole(rel));
	float in = $in, lo = $lo, hi = $hi;
	int   gr = in < lo ? -1 :
		   in > hi ? +1 :
		   0;

//	$lo = in < lo ? in : hi + p * (lo - hi);
//	$hi = in > hi ? in : lo + p * (hi - lo);

	float ct = (lo + hi)/2;
	$lo = gr < 0 ? in : ct + p * (lo - ct);
	$hi = gr > 0 ? in : ct + p * (hi - ct);

	[gr, =$lo, =$hi];
};

// ----------------------------------------------------------------------------
__pick_peaks = FPP(i_extr, i_peak) _int_,unsigned,float {
FILE:	#include <$_INCDIR_/fpp.h>
decl:	int		$s_extr = 0; // reset
	unsigned	$s_time;
	float		$s_peak;

stmt:	unsigned	$r_time _unused_;
	float		$r_peak _unused_;

exec:	int i_extr = $i_extr, r_extr = 0;
	float i_peak = $i_peak;

	if (!i_extr) {
		goto ret;
	} else if (i_extr != $s_extr) {
		r_extr = $s_extr; $r_time = $s_time; $r_peak = $s_peak;
		$s_extr = i_extr;
	} else if (i_extr != signum(i_peak - $s_peak)) {
		goto ret;
	}

	$s_time = $(u.iota); $s_peak = i_peak;
	ret: [r_extr, =$r_time, =$r_peak]
};

pick_peaks_bpf(rel) = _ <: (env_foll2(rel) : _,!,!), _ : __pick_peaks;

__pick_turns = FPP(rel,sens, i_extr, i_peak) _int_,unsigned,float {
FILE:	#include <$_INCDIR_/fpp.h>
decl:	int		$s_extr = 0; // reset
	unsigned	$s_time = 0;
	float		$s_peak = 0;
	float		$s_leap = 0;

stmt:	unsigned	$r_time _unused_;
	float		$r_peak _unused_;

exec:	int i_extr = $i_extr, r_extr = 0;
	unsigned i_time = $(u.iota) - 1;
	float i_peak = $i_peak;

	float	__leap = i_peak - $s_peak,
		i_leap = fabsf(__leap);

	if (!i_extr)
		;
	else if (i_extr == $s_extr) {
		if (signum(i_extr) == signum(__leap)) {
			$s_time = i_time;
			$s_peak = i_peak;
		}
	} else if (i_leap > $s_leap) {
		$s_leap = fmaxf($s_leap, i_leap * $sens);
		$r_time = $s_time; $r_peak = $s_peak;
		$s_time =  i_time; $s_peak =  i_peak;
		$s_extr =  r_extr = i_extr;
	}

	$s_leap *= $(ba.tau2pole(rel));
	[r_extr, =$r_time, =$r_peak];
};

pick_turns_bpf(rel,sens) = _ <: extremum, mem : __pick_turns(rel, sens);

// ----------------------------------------------------------------------------
__extr_dt = FPP(extr, time, peak) pure _int_,unsigned,float {
decl:	unsigned $t_lo = 0, $t_hi = 0;
exec:	unsigned dt = 0; // for -Wmaybe-uninitialized

	$!CK_PRIM($extr, $time, $peak);

	if ($extr < 0) {
		dt = $time - $t_lo; $t_lo = $time;
	} else if ($extr > 0) {
		dt = $time - $t_hi; $t_hi = $time;
	}

	[=$extr, dt, =$peak]
};

pick_peaks = pick_peaks_bpf : __extr_dt;
pick_turns = pick_turns_bpf : __extr_dt;

// ----------------------------------------------------------------------------
extr_dt_st(s, xt,dt,pk) = (xt<0,s,dt : smoo_stat),
			  (xt>0,s,dt : smoo_stat);

__extr_dt_bpf2 = si.bus(3) <: (!,_,!), (__extr_dt : _,_,!)
	       : \(time,xt,dt).(xt<0,time,dt, xt>0,time,dt);

// ==> : par(i,2, plot_bpf(LAG))
pick_peaks_dt_bpf2 = pick_peaks_bpf : __extr_dt_bpf2;
pick_turns_dt_bpf2 = pick_turns_bpf : __extr_dt_bpf2;


__plot_bpf = FPP(HOW,LAG, push,time,val) {
FILE:	#include <queue>
	struct $tv { unsigned time; float val; };
	static int $ID;

decl:	std::queue<$tv> $q;
	int	$t0, $t1 = 0;
	float	$v0, $v1 = 0;
	int	$id = ++$ID;

exec:	if ($push) $q.push({$time, float($val)});

	float ret = $v1;
	int time = $(u.iota) - $LAG;
	if (time < 0) goto $ret;

	while (time >= $t1) {
		static int warned;
		if ($q.empty()) {
			if (!warned++)
				LOG("WARN! bpf[%d]: empty %d [%d %d]",
				    $id, time, $t0,$t1);
			goto $ret;
		}
		$t0 = $t1; $v0 = $v1;
		auto n = $q.front(); $q.pop();
		$t1 = n.time; $v1 = n.val;
		warned = 0;
	}

	switch ($HOW) {
	case 0:
		ret = time == $t0 ? $v0 : 0;
		break;
	case 1:
		ret = $v0 + float(time - $t0) / float($t1 - $t0) * ($v1 - $v0);
		break;
	default:
		assert(0);
	}

	$ret: ret;
};

plot_bpf = __plot_bpf(1);
