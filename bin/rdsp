#!/usr/bin/perl -w
use strict;

my ($TGT, $FLT, $DSP) = ('cli', '-single');

while (defined($_ = shift)) {
	if (/^-(cli|gtk|pagtk)$/)		{ $TGT = $1; }
	elsif (/^-(double|quad)$/)	{ $FLT = $_; }
	elsif (s/\.dsp$//)		{ $DSP = $_; last; }
	else {
		die "ERR!! unsupported option '$_'.\n";
	}
}

$DSP // die "ERR!! no .dsp files specified.\n";

sub do_which
{
	my $name = shift;
	-e and return -l ? readlink : $_
		for map "$_/$name", split ':', $ENV{PATH};
	die "ERR!! can't find '$name' in $ENV{PATH}\n";
}
sub which
{
	my ($name, $chop) = @_;
	$name = do_which $name;
	$name, $name =~ s|(?:/[^/]+){$chop}$||r;
}

my ($FPP_BIN, $FPP_DIR) = which 'fpp', 2;
my @F_DEPS = map "$_/*", grep -d $_, map "$FPP_DIR/$_", qw(lib arch);
my @C_DEPS = map "$_/*", grep -d $_, map "$FPP_DIR/$_", qw(include);

my $FPP_CMD = "fpp $FLT -it -ct 0";
my $CPP_CMD = "\$(CXX) -std=c++14 -Wall -Ofast -lsndfile";

my ($FAUST_BIN, $FAUST_DIR) = which 'faust', 3; # /build/bin/faust
my ($FAUST_LIB, $FAUST_ARC) = map "$FAUST_DIR/$_", qw(libraries architecture);
-d $FAUST_LIB and $FPP_CMD .= " -I $FAUST_LIB";
-d $FAUST_ARC and $FPP_CMD .= " -A $FAUST_ARC",
		  $CPP_CMD .= " -I $FAUST_ARC";

my ($BIN, $CLI_FLT) = ("$DSP-$TGT", '');

if    ($FLT eq '-single' or $TGT ne 'cli') {}
elsif ($FLT eq '-double') { $BIN .= 'd'; $CLI_FLT = '-D FAUSTFLOAT=double'; }
elsif ($FLT eq '-quad')	  { $BIN .= 'q'; $CLI_FLT = '-D FAUSTFLOAT=quad';   }

my $MAKEF = {cli=> <<CLI, gtk=> <<GTK, pagtk=> <<PAGTK}->{$TGT};
.INTERMEDIATE: $BIN.cpp

$BIN: $BIN.cpp @C_DEPS
	$CPP_CMD $CLI_FLT -lpthread -lreadline \$< -o \$@

$BIN.cpp: $DSP.dsp $FPP_BIN @F_DEPS
	$FPP_CMD -scn '' -nvi -a cli.cpp \$< -o \$@
CLI
.INTERMEDIATE: $BIN.cpp

$BIN: $BIN.cpp @C_DEPS
	$CPP_CMD -w -D__PresetUI_H__ `pkg-config --cflags --libs gtk+-2.0` \$< -o \$@

$BIN.cpp: $DSP.dsp $FPP_BIN @F_DEPS
	$FPP_CMD -a ma-gtk.cpp \$< -o \$@
GTK
.INTERMEDIATE: $BIN.cpp

$BIN: $BIN.cpp @C_DEPS
	$CPP_CMD -w `pkg-config --cflags --libs portaudiocpp gtk+-2.0` \$< -o \$@

$BIN.cpp: $DSP.dsp $FPP_BIN @F_DEPS
	$FPP_CMD -a pa-gtk.cpp \$< -o \$@
PAGTK

open my $fd, '|-', 'make', qw(-s -f -) or die;
print $fd $MAKEF; close $fd or exit;

$ENV{HOME} = '/tmp'; exec $BIN, @ARGV;
